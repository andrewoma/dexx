buildscript {
    ext {
        kotlinVersion = '1.0.0'
        junitVersion = '4.12'
        guavaVersion = '14.0.1'
        assertJVersion = '1.7.0'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
                'com.bmuschko:gradle-nexus-plugin:2.3.1',
                'net.researchgate:gradle-release:2.1.2',
                'com.github.ben-manes:gradle-versions-plugin:0.11.3',
                'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
        )
    }
}

apply plugin: 'net.researchgate.release'

createReleaseTag.dependsOn ':collection:uploadArchives', ':kollection:uploadArchives'

allprojects {
    group = 'com.github.andrewoma.dexx'
    apply plugin: 'com.github.ben-manes.versions'

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'kotlin'
    apply from: '../publish.gradle'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions.moduleName = "${project.group}.${project.name}"
    }

    test {
        exclude '**/Abstract*'
    }
}


project(':collection') {

    apply plugin: 'nebula.optional-base'
    apply plugin: "jacoco"
    
    jar {
        manifest { // the manifest of the default jar is of type OsgiManifest
            name = 'Dexx Collections'
            instruction 'Export-Package',
                'com.github.andrewoma.dexx.collection',
                '!com.github.andrewoma.dexx.collection.internal'
        }
    }

    dependencies {
        compile "com.intellij:annotations:12.0", optional
        testCompile(
                "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
                "junit:junit:$junitVersion",
                "com.google.guava:guava:$guavaVersion",
        )
    }

    jacoco {
        toolVersion = "0.7.5.201505241946"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    test {
        if (project.hasProperty('dexxTestMode')) {
            maxHeapSize = '2048m'
            jvmArgs '-server'
        }

        systemProperty "dexx.test.mode", project.properties['dexxTestMode'] ?: "QUICK"
    }
}

project(':kollection') {

    dependencies {
        compile(
                project(':collection'),
                "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        )
        testCompile(
                "junit:junit:$junitVersion",
                "org.assertj:assertj-core:$assertJVersion",
                "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
